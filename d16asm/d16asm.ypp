%{
#include "d16asm.h"

list<dwPayload> listPayload;

%}

%union {
	long l;
	char *s;
}

%token <l> TOK_NUMBER
%token <s> TOK_IDENTIFIER TOK_LITERAL TOK_LABEL
%token TOK_EOL
/* directives */
%token TOK_ORG TOK_EQU TOK_DW TOK_DS
/* keywords */
%token TOK_DROP TOK_JMP TOK_CALL TOK_RET TOK_DUP
%token TOK_LOAD TOK_STORE TOK_JMPZ TOK_JMPNZ TOK_LSR TOK_LSL
%token TOK_ADD TOK_ADC TOK_SUB TOK_SBC

%start S

%%

S:
 | S line
 ;


line: TOK_EOL
    | keyword
    | TOK_NUMBER     { addNumber($1); }
    | TOK_LABEL      { addLabel($1); }
    | TOK_IDENTIFIER { addIdentifier($1); }
    | directive
    ;


keyword: TOK_DROP   { addKeyword(d16::OPCODES::DROP); }
       | TOK_JMP    { addKeyword(d16::OPCODES::JMP); }
       | TOK_CALL   { addKeyword(d16::OPCODES::CALL); }
       | TOK_RET    { addKeyword(d16::OPCODES::RET); }
       | TOK_DUP    { addKeyword(d16::OPCODES::DUP); }
       | TOK_LOAD   { addKeyword(d16::OPCODES::LOAD); }
       | TOK_STORE  { addKeyword(d16::OPCODES::STORE); }
       | TOK_JMPZ   { addKeyword(d16::OPCODES::JMPZ); }
       | TOK_JMPNZ  { addKeyword(d16::OPCODES::JMPNZ); }
       | TOK_LSR    { addKeyword(d16::OPCODES::LSR); }
       | TOK_LSL    { addKeyword(d16::OPCODES::LSL); }
       | TOK_ADD    { addKeyword(d16::OPCODES::ADD); }
       | TOK_ADC    { addKeyword(d16::OPCODES::ADC); }
       | TOK_SUB    { addKeyword(d16::OPCODES::SUB); }
       | TOK_SBC    { addKeyword(d16::OPCODES::SBC); }
       ;


directive: TOK_ORG TOK_NUMBER TOK_EOL { addOrg( $2 ); }
         | TOK_DS TOK_NUMBER TOK_EOL  { addDs($2); }
         | equ
         | dw
         ;


equ: TOK_EQU TOK_IDENTIFIER TOK_NUMBER TOK_EOL { addEqu( $2, $3 ); }
   ;


dw: TOK_DW dwdata { addDw(); listPayload.clear(); }
  ;


dwdata: TOK_NUMBER                  { listPayload.push_back( dwPayload($1) ); }
      | TOK_LITERAL                 { listPayload.push_back( dwPayload($1, dwPayload::Literal) ); }
      | TOK_IDENTIFIER              { listPayload.push_back( dwPayload($1, dwPayload::Ident) ); }
      | dwdata ',' TOK_NUMBER       { listPayload.push_back( dwPayload($3) ); }
      | dwdata ',' TOK_LITERAL      { listPayload.push_back( dwPayload($3, dwPayload::Literal) ); }
      | dwdata ',' TOK_IDENTIFIER   { listPayload.push_back( dwPayload($3, dwPayload::Ident) ); }
      ;

%%

void yyerror(char *s, ...) {
    va_list ap;
    va_start(ap, s);
    fprintf(stderr, "%d: error: ", yylineno);
    vfprintf(stderr, s, ap);
    fprintf(stderr, "\n");
    va_end(ap);
}
